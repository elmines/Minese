program : optDefs

optDefs : NONE
	| defs

defs : definition
     | definition defs

definition : classDef
           | functionDef
           | varDef



classDef : CLASS IDENTIFIER optExtension OCURLY classBody CCURLY

optExtension : NONE
             | EXTENDS IDENTIFIER

classBody : definition
          | definition classBody



functionDef : DEFINE IDENTIFIER funcBody

funcBody: OPAREN optParamsList CPAREN block

optParamsList : NONE
              | paramsList

paramsList : VAR IDENTIFIER
           | VAR IDENTIFIER COMMA paramsList



varDef : VAR IDENTIFIER optAssignment SEMICOLON

optAssignment : NONE
              | ASSIGN expression



block : OCURLY optStatements CCURLY
      | statement

optStatements : NONE
              | statements

statements : statement
           | statement statements

statement : definition
	  | condBlock
          | whileBlock
          | returnStatement
          | expression  SEMICOLON



condStatement : IF OPAREN expression CPAREN block |
              : IF OPAREN expression CPAREN block ELSE block

whileStatement : WHILE OPAREN expression CPAREN block



returnStatement : RETURN expression SEMICOLON
                | RETURN SEMICOLON


expression : unary
           | unary funcCall optOperation
           | unary index    optOperation
           | unary operator expression

optOperation: NONE
	    | operator expression

funcCall: OPAREN optExprList CPAREN

optExprList : NONE
            | exprList

exprList : expression
         | expression COMMA exprList

index:    OBRACK expression CBRACK



unary: UMINUS unary
     | NOT    unary
     | OPAREN expression CPAREN
     | array
     | anonFunction
     | IDENTIFIER
     | boolean
     | INTEGER
     | STRING

array : OBRACK exprList CBRACK

anonFunction : LAMBDA funcBody

boolean : TRUE
        | FALSE

operator : PLUS
         | TIMES
         | MINUS
         | DIV
         | MOD
         | LT
         | LTE
	 | NEQ
         | EQ
         | GTE
         | GT
         | ASSIGN
         | DOT
         | AND
         | OR
