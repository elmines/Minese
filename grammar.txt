program : definition
        | definition program

definition : var_def
           | class_def
           | function_def

var_def: VAR IDENTIFIER

class_def: class_header LEFT_CURLY class_body RIGHT_CURLY
class_header: CLASS IDENTIFIER | CLASS IDENTIFIER EXTENDS IDENTIFIER
class_body: member_def | member_def class_body
member_def: var_def | function_def


function_def: IDENTIFIER LEFT_PAREN optParamsList RIGHT_PAREN block
optParamsList: NONE | paramsList
paramsList: IDENTIFIER | IDENTIFIER COMMA paramsList




block: LEFT_CURLY optStatements RIGHT_CURLY
optStatements: NONE | statements
statements: statement | statement statements
statement: cond_block
         | while_block
         | expression SEMICOLON
         | function_def SEMICOLON
expression: unary | unary operator expression
operator: PLUS | TIMES | MINUS | DIV | MOD | LT | LTE | EQ | GTE | GT | GETS

unary: MINUS unary
     | function_call
     | IDENTIFIER
     | INTEGER
     | string
     | array
     | array_element
     | closure

function_call: IDENTIFIER LEFT_PAREN optExprList RIGHT_PAREN
optExprList: NONE | exprList
exprList: expression | expression COMMA exprList

string: LEFT_QUOTE opt_tokens RIGHT_QUOTE
opt_tokens: NONE | tokens
tokens: TOKEN | TOKEN tokens

array: LEFT_CURLY exprList RIGHT_CURLY
array_element: IDENTIFIER LEFT_BRACK INTEGER RIGHT_BRACK

closure: closure_header block | closure_header expression
closure_header: LEFT_PAREN optParamsList RIGHT_PAREN ARROW

cond_block: if_statement opt_else_statement

if_statement: if_header block | if_header statement
if_header: IF LEFT_PAREN expression RIGHT_PAREN

opt_else_statement: NONE | else_statement
else_statement: ELSE block | ELSE statement

while_block: while_header block | while_header statement
while_header: WHILE LEFT_PAREN expression RIGHT_PAREN
